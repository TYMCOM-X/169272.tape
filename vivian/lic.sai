entry Prt.Lic, Rd.Lic, Oper.Lic, Set.Lic, Frame.Lic, Proc.Lic, Get.Priv;
define ! = "COMMENT", !! = "COMMENT";
!! title DA Lic ;
! :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:::::::::::::::::::::::::::::::::::::+:::::::::::::::::::::::::::::::::::::
:                                                                         :
:                                                                         :
:				    Lic					  :
:                                                                         :
:                                                                         :
:::::::::::::::::::::::::::::::::::::+:::::::::::::::::::::::::::::::::::::


		   License- and privilege-related stuff.


::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;


begin "Lic";
  require "(Sailib)Sail.Def" source!file;
  require "<><>" delimiters;

  define
    ht = <#ht>,
    cr = <#cr>,
    rh = <!rh>,
    lh = <!lh>,
    inc (X, Y) = <X _ X + ifc (length(cvps(Y))) thenc Y elsec 1 endc>,
    dcr (X, Y) = <X _ X - ifc (length(cvps(Y))) thenc Y elsec 1 endc>;


  ! The sequence of license bits listed below comes from the estimable
    sources of SETOP ;

  define
    Lic.Siz = 28;

  define
    ##WC = !bit(18),  ##RC = !bit(19),  ##OP = !bit(20),  ##SY = !bit(21),
    ##GD = !bit(22),  ##TD = !bit(23),  ##ST = !bit(24),  ##HF = !bit(25),
    ##JL = !bit(26),  ##AC = !bit(27),  ##XC = !bit(28),  ###1 = !bit(29),
    ###2 = !bit(30),  ###3 = !bit(31),
    ##RA = <##RF lor ##RP>,
    ##RF = !bit(32),  ##RP = !bit(33),
    ##WA = <##WF lor ##WP>,
    ##WF = !bit(34),  ##WP = !bit(35),
    ##MG = !bit(17),  ##HU = !bit(16),  ##CS = !bit(15),  ##AP = !bit(14),
    ##MT = !bit(13),  ##SP = !bit(12),  ##RL = !bit(11),  ##MN = !bit(10);

  Preset!with
    "WC", "RC", "OP", "SY",
    "GD", "TD", "ST", "HF",
    "JL", "AC", "XC", "*1",
    "*2", "*3",
    "RA",
    "RF", "RP",
    "WA",
    "WF", "WP",
    "MG", "HU", "CS", "AP",
    "MT", "SP", "RL", "MN";

  safe string array Lic.Nam [1:Lic.Siz];

  Preset!with
    ##WC, ##RC, ##OP, ##SY,
    ##GD, ##TD, ##ST, ##HF,
    ##JL, ##AC, ##XC, ###1,
    ###2, ###3,
    ##RA,
    ##RF, ##RP,
    ##WA,
    ##WF, ##WP,
    ##MG, ##HU, ##CS, ##AP,
    ##MT, ##SP, ##RL, ##MN;

  safe integer array Lic [1:Lic.Siz];


! ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
!! title Rtn GetTab ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn GetTab				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple integer procedure GetTab (integer Index, Table);
  begin "GetTab"
    own integer
      Val;


    start!code
      move	1, Table;
      hrl	1, Index;
      setom	!skip!;
      calli	1, '41;
       setzm	!skip!;
      movem	1, Val;
    end;

    return(Val);
  end "GetTab";


! ===================================+=====================================
==========================================================================;
!! title Rtn Prt.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Prt.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return a string containing a list of the licenses present in the
	input.

	(The method used here depends upon composite licenses preceding
	the individual licenses of which they are composed in the
	enumeration sequence.)


==========================================================================;


  internal simple string procedure Prt.Lic (integer License);
  begin "Prt.Lic"
    string
      S;
    integer
      I;

    S _ null;

    for I _ 1 step 1 until Lic.Siz do
      if (License land Lic[I] = Lic[I]) then
      begin
	S _ S & " " & Lic.Nam[I];
	License _ License land lnot Lic[I];
      end;

    return(S[2 for inf-1]);
  end "Prt.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Rd.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Rd.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return an integer with the proper bits set to represent the
	licenses enumerated in the input.


==========================================================================;


  internal simple integer procedure Rd.Lic (string License);
  begin "Rd.Lic"
    integer
      L,
      I,
      Itable,
      Brk;
    string
      S;

    setbreak(Itable _ getbreak, " ,:;." & ht, " ,:;." & ht, "KINST");
    L _ 0;

    while (length(S _ Scan(License, Itable, Brk))) do
      for I _ 1 step 1 until Lic.Siz do
	if (kequ(S, Lic.Nam[I])) then
	begin
	  L _ L lor Lic[I];
	  done;
	end;

    relbreak(Itable);
    return(L);
  end "Rd.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Cv.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Cv.Name				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  long real procedure Cv.Name (string Name);
  begin "Cv.Name"
    integer C;
    integer NM1;
    integer NM2;
    long real L;


! ========================================================================;
!! title SubRtn Radix.45 ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      SubRtn Radix.45				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple integer procedure Radix.45 (integer Ch);
  begin "Radix.45"
    if (Ch >= "A" and Ch <= "Z") then
      return(Ch + 1 - "A");

    if (Ch >= "a" and Ch <= "z") then
      return(Ch + 1 - "a");

    if (Ch >= "0" and Ch <= "9") then
      return(Ch + 31 - "0");

    return(
	case Ch of
	(
	  [" "]  0,
	  ["-"]  28,
	  ["."]  29,
	  ["/"]  30,
	  [":"]  41,
	  ["*"]
	  ["@"]  42,
	  ["'"]  43,
	  else   44
	)
    );
  end "Radix.45";


! ===================================+=====================================
==========================================================================;
!! title Body Cv.Name ;
! ========================================================================;


    Name _ (Name & "            ")[1 for 12];
    NM1  _ NM2 _ 0;

    while (length(Name)) do
    begin
      C _ Radix.45(lop(Name));

      start!code
	MOVE	3, NM1;
	MOVE	1, NM2;
	IMULI	3, 45;
	MULI	1, 45;
	ADD	1, 3;
	ADD	2, C;
	MOVEM	1, NM1;
	MOVEM	2, NM2;
      end;
    end;

    memory[location(L)]   _ NM1;
    memory[location(L)+1] _ NM2;
    return(L);
  end "Cv.Name";


! ===================================+============================================================================================================;
!! title Rtn Cv.Hash ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Cv.Hash				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  integer procedure Cv.Hash (string PW);
  begin "Cv.Hash"
    integer C;
    integer Fives;
    integer T1;
    integer T2;
    integer T3;
    integer T4;
    integer array X[0:4];


    X[0]  _ X[2] _ X[4] _ '555555555555;
    X[1]  _ X[3] _ '361275431652;
    Fives _ '343277244615;
    T3    _ 0;

    while (length(PW)) do
    begin "PSWLP"
      C    _ lop(PW);
      inc(X[0], C);
      inc(X[3], C);
      T4   _ '40;

      do
      begin "RAND"
	T1 _ X[T3];

	start!code
	  MOVE	1, T1;
	  MUL	1, Fives;
	  MOVEM	1, T1;
	  MOVEM	2, T2;
	end;

	inc(T3);
	inc(X[T3], T2);

	if (T3 = 4) then
	begin
	  T3   _ X[4] lsh -'42;
	  inc(X[0], X[4]);
	end;
      end "RAND"
      until ((dcr(T4)) <= 0);
    end "PSWLP";

    return(X[0]);
  end "Cv.Hash";


! ===================================+=====================================
==========================================================================;
!! title Rtn Get.Line ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Get.Line				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  string procedure Get.Line;
  begin "Get.Line"
    integer I;
    string  S;


    S _ null;

    do
    begin
      I _ auxclv(-1, 0, '46);		! input a character;

      if (I >= '173) then
	done;				! these are line breaks (ugh);

      if (I >= '140) then
	dcr(I, '40);			! convert lower to upper case;

      if (I >= '40) then
	S _ S & I;			! add to string if legal;
    end
    until (I < '40);

    if (I = cr) then
      auxclv(-1, 0, '46);		! If CR then read the LF;

    auxclv(-1, 0, '11);			! Try and get node to echo;
    return(S);
  end "Get.Line";


! ===================================+=====================================
==========================================================================;
!! title Rtn Oper.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Oper.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  string Oper.Name;


! ========================================================================;


  internal integer procedure Oper.Lic (string OPN(null));
  begin "Oper.Lic"
    long real Name;
    integer Hash;
    integer Lic;
    integer Ptr;
    integer OP;
    integer EOF;
    integer array Buf[0:'777];
    string  Tmp;

    define
      bomb (X) =
      <
	begin
	  print(X, crlf);
	  release(OP);
	  Oper.Name _ null;
	  return(0)
	end
      >;

    define
      Bfnm1 = 0,
      Bfnm2 = 1,
      Bfpsw = 2,
      Bflic = 3,
      Nm1   = <location(Name)>,
      Nm2   = <location(Name)+1>;

    if (length(OPN)) then
      Tmp _ OPN
    else
      while (true) do
      begin
	print("Oper name: ");
	Tmp _ Get.Line;

	if (Tmp = "?") then
	  print("Enter your name as it appears in the OPER",
	      " license file.", crlf)
	else
	  done;
      end;

    if (length(Tmp) = 0) then
      bomb("?no name given");

    Name _ Cv.Name(Oper.Name _ Tmp);
    auxclr(-1, memory['200], '64);

    while (true) do
    begin
      print("Password: ");
      Tmp _ Get.Line;

      if (Tmp = "?") then
	print("?", crlf,
	    "Enter your password associated with the OPER name ",
	    Oper.Name, ".", crlf)
      else
	done;
    end;

    Hash _ Cv.Hash(Tmp);
    print(crlf);
    auxclr(-1, memory[0], '64);
    auxclv(-1, 0, '11);
    open(OP _ getchan, "SYS", '17, 0, 0, 0, 0, EOF _ true);

    if (EOF) then
      bomb("?Can't init SYS:");

    begin "Lookup"
      define !CHLK = '4;	! CHANIO lookup function;
      define !RBLic = '14;	! RIB address of file license;
      integer array XL[0:!RBLic];

      XL[0] _ arrinfo(XL, 2);
      XL[2] _ cvsix("DRWSPR");
      XL[3] _ cvsix("EPO");
      chnior(OP, XL[0], !CHLK);

      if (not !SKIP!) then
	bomb("?OPER password file not found");

      if ((XL[!RBLic] land '400000) = 0) then 
	bomb("?OPER password file has no license");
    end "Lookup";

    do
    begin "Search"
      arryin(OP, Buf[0], '1000);

      if (EOF) then
	done "Search";

      for Ptr _ 0 step 7 until '770 do
      begin "Try"
	if (Buf[Ptr + Bfnm1] neq memory[Nm1]) then
	  continue "Try";

	if (Buf[Ptr + Bfnm2] neq memory[Nm2]) then
	  continue "Try";

	EOF _ Buf[Ptr + Bfpsw] neq Hash;
	Lic _ Buf[Ptr + Bflic];
	done "Search";
      end "Try";
    end "Search"
    until (EOF);

    close(OP);
    release(OP);

    if (EOF) then
    begin
      print("?Bad name or password", crlf);
      return(0);
    end;

    return(Lic);
  end "Oper.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Set.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Set.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	set license stuff (ech):
	Set.Lic(license)


==========================================================================;


  internal simple procedure Set.Lic (integer Lic);
  begin "Set.Lic"
    if (calli(-'25, '41) = calli(-'23, '41)) then
      calli(Lic, -'10);
  end "Set.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Frame.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Frame.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the frame license value for a given frame.


==========================================================================;


  internal simple integer procedure Frame.Lic (integer Frame(-1));
  begin "Frame.Lic"
    return(rh(GetTab(Frame, -'20)));
  end "Frame.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Proc.Lic ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Proc.Lic				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the process license value for a given frame.


==========================================================================;


  internal simple integer procedure Proc.Lic (integer Frame(-1));
  begin "Proc.Lic"
    return(lh(GetTab(Frame, -'20)));
  end "Proc.Lic";


! ===================================+=====================================
==========================================================================;
!! title Rtn Get.Priv ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Get.Priv				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the privilege word for a given frame.


==========================================================================;


  internal simple integer procedure Get.Priv (integer Frame(-1));
  begin "Get.Priv"
    return(GetTab(Frame, '6));
  end "Get.Priv";


! ===================================+=====================================
==========================================================================;

end "Lic";

! :::::::::::::::::::::::::::::::::::+:::::::::::::::::::::::::::::::::::::
				  DA Lic
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
/Gì