TITLE TFMT.  V.032(401)    T FORMAT FORTRAN IV
SUBTTL	18-APR-72	/DMN
;***COPYRIGHT 1969,1970,1971,1972 DIGITAL EQUIPMENT CORP., MAYNARD, MASS.***

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;THIS SUBROUTINE INTERPRETS THE "T" FORMAT CONTROL
;IN FORTRAN IV

;THE SPACING COUNT IS CONTAINED IN AC A UPON ENTERING THE 
;ROUTINE.

;THE ROUTINE IS CALLED BY "FORSE."  .

ENTRY	TFMT.
EXTERN	XIO.,EOL.,RIN.,TPNTR.,TCNT2.,TCNT1.,ERROR.

;ACCUMULATORS
	A=1			;UTILITY
	B=2			;UTILITY
	C=3			;UTILITY
	D=4			;UTILITY
	M=7			;ADDR. OF BUFFER HEADER(RH)
	I=11			;FLAG REGISTER
	P=17			;PUSHDOWN POINTER
IFN REENT,<LOW=16>
IFE REENT,<LOW=0>

TFMT.:	POP	P,0		;DUMMY POP
	SUBI	A,1		;ADJUST COUNT
	TLNE	I,400000	;OUTPUT?
	JUMPLE	A,ERROR1	;YES, ACCOUNT FOR CARRIAGE CONTROL CHAR.
	SUB	A,EOL.(LOW)	;SUBTRACT COLUMN NO.
	SOJG	A,XIO.		;GO TO X FORMAT WITH COUNT IN A
	JUMPE	A,RIN.		;IF COUNT IS ZERO JUST RETURN
	MOVE	0,EOL.(LOW)	;GET CURRENT LINE POSITION
	TLON	I,4		;HAS THERE BEEN A PREVIOUS T FORMAT
	JRST	SAVE		;SAVE OLD VALUES BEFORE BACKUP
	CAIG	0,TCNT1.(LOW)	;IS CURRENT POSITION AFTER SAVED POSITION
	JRST	NOSAVE		;NO. DON'T SAVE CURRENT
SAVE:	MOVEM	0,TCNT1.(LOW)	;SAVE LINE POSITION
	MOVE	0,1(M)		;CURRENT BUFFER POINTER
	MOVEM	0,TPNTR.(LOW)
	MOVE	0,2(M)		;BUFFER COUNT
	MOVEM	0,TCNT2.(LOW)
NOSAVE:	ADDM	A,EOL.(LOW)	;ADJUST LINE POSITION
        MOVE    0,A             ; ADJUST BUFFER COUNT. TAKE INTO ACCOUNT
        SKIPGE  0               ; THE FACT THAT WE MAY HAVE A NEG NUMBER IN
        SETCA   0,              ; A, AND WE NEED TO ADJUST THINGS THE RIGHT
        ADDM    0,2(M)          ; WAY.
	IDIVI	A,5		;CONVERT TO WORD A CHAR COUNT
	ADDM	A,1(M)		;ADJUST CHARACTER POINTER
	JUMPE	B,RIN.		;EXIT IF NO REMAINDER
	SOS	1(M)		;MOVE BACK ONE MORE WORD DUE TO NEG. REMAINDER
	ADDI	B,5		;MAKE REMAINDER POSITIVE
	IBP	1(M)		;ADJUST POINTER BY EXTRA CHARACTERS
	SOJG	B,.-1
	JRST	RIN.

;HERE WHEN T1 SEEN IN OUTPUT FORMAT
;THIS IS ILLEGAL SINCE TX ON OUTPUT  POSITIONS ON X-1
;AND COLUMN 0 DOES NOT  EXIST
;GIVE ILL CHAR IN FORMAT MESSAGE
ERROR1:	MOVEI	2,'1'(A)	;FAKE ILL CHAR
	PUSHJ	P,ERROR.	;NEVER RETURN

	END
